// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext.kotlin_version = "1.4.10"
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath "com.android.tools.build:gradle:4.1.0"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
    maven {
      url "$rootDir/../node_modules/react-native/android"
    }
    maven {
      url("$rootDir/../node_modules/jsc-android/dist")
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

String genVersionName(int versionCode) {
  def pattern = java.util.regex.Pattern.compile("(\\d+)(\\d{2})(\\d{2})(\\d{2})")
  def matcher = pattern.matcher(versionCode.toString())
  if (matcher.matches()) {
    // 主版本号
    def mainVersionCode = matcher.group(1).toInteger()
    // 次版本号
    def secondaryVersionCode = matcher.group(2).toInteger()
    // 小版本号
    def lastVersionCode = matcher.group(3).toInteger()
    // 修复版本号
    def fixVersionCode = matcher.group(4).toInteger()
    return "$mainVersionCode.$secondaryVersionCode.$lastVersionCode.$fixVersionCode"
  }
  return versionCode.toString()
}

ext {
  applicationId = "com.xjw.wanwan"
  debugApplicationId = applicationId + ".debug"
  generateApkName = "wanwanRCTApp"

  compileSdkVersion = 30
  buildToolsVersion = "30.0.2"
  minSdkVersion = 21
  targetSdkVersion = 30
  versionCode = 1000000
  versionName = genVersionName(versionCode)

  kotlin = "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  ktx = "androidx.core:core-ktx:1.2.0"
  appcompat = "androidx.appcompat:appcompat:1.1.0"
  material = "com.google.android.material:material:1.1.0"
  constraintLayout = "androidx.constraintlayout:constraintlayout:1.1.3"
  // react native
  rct = "com.facebook.react:react-native:+"
  jsc = "org.webkit:android-jsc:+"
}